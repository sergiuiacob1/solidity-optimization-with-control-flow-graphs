{{
  "language": "Solidity",
  "settings": {
    "evmVersion": "berlin",
    "libraries": {},
    "metadata": {
      "bytecodeHash": "ipfs",
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 10000
    },
    "remappings": [],
    "outputSelection": {
      "*": {
        "*": [
          "evm.bytecode",
          "evm.deployedBytecode",
          "devdoc",
          "userdoc",
          "metadata",
          "abi"
        ]
      }
    }
  },
  "sources": {
    "@openzeppelin/contracts/access/Ownable.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../utils/Context.sol\";\n\n/**\n * @dev Contract module which provides a basic access control mechanism, where\n * there is an account (an owner) that can be granted exclusive access to\n * specific functions.\n *\n * By default, the owner account will be the one that deploys the contract. This\n * can later be changed with {transferOwnership}.\n *\n * This module is used through inheritance. It will make available the modifier\n * `onlyOwner`, which can be applied to your functions to restrict their use to\n * the owner.\n */\nabstract contract Ownable is Context {\n    address private _owner;\n\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\n\n    /**\n     * @dev Initializes the contract setting the deployer as the initial owner.\n     */\n    constructor() {\n        _transferOwnership(_msgSender());\n    }\n\n    /**\n     * @dev Returns the address of the current owner.\n     */\n    function owner() public view virtual returns (address) {\n        return _owner;\n    }\n\n    /**\n     * @dev Throws if called by any account other than the owner.\n     */\n    modifier onlyOwner() {\n        require(owner() == _msgSender(), \"Ownable: caller is not the owner\");\n        _;\n    }\n\n    /**\n     * @dev Leaves the contract without owner. It will not be possible to call\n     * `onlyOwner` functions anymore. Can only be called by the current owner.\n     *\n     * NOTE: Renouncing ownership will leave the contract without an owner,\n     * thereby removing any functionality that is only available to the owner.\n     */\n    function renounceOwnership() public virtual onlyOwner {\n        _transferOwnership(address(0));\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Can only be called by the current owner.\n     */\n    function transferOwnership(address newOwner) public virtual onlyOwner {\n        require(newOwner != address(0), \"Ownable: new owner is the zero address\");\n        _transferOwnership(newOwner);\n    }\n\n    /**\n     * @dev Transfers ownership of the contract to a new account (`newOwner`).\n     * Internal function without access restriction.\n     */\n    function _transferOwnership(address newOwner) internal virtual {\n        address oldOwner = _owner;\n        _owner = newOwner;\n        emit OwnershipTransferred(oldOwner, newOwner);\n    }\n}\n"
    },
    "@openzeppelin/contracts/security/ReentrancyGuard.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Contract module that helps prevent reentrant calls to a function.\n *\n * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier\n * available, which can be applied to functions to make sure there are no nested\n * (reentrant) calls to them.\n *\n * Note that because there is a single `nonReentrant` guard, functions marked as\n * `nonReentrant` may not call one another. This can be worked around by making\n * those functions `private`, and then adding `external` `nonReentrant` entry\n * points to them.\n *\n * TIP: If you would like to learn more about reentrancy and alternative ways\n * to protect against it, check out our blog post\n * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].\n */\nabstract contract ReentrancyGuard {\n    // Booleans are more expensive than uint256 or any type that takes up a full\n    // word because each write operation emits an extra SLOAD to first read the\n    // slot's contents, replace the bits taken up by the boolean, and then write\n    // back. This is the compiler's defense against contract upgrades and\n    // pointer aliasing, and it cannot be disabled.\n\n    // The values being non-zero value makes deployment a bit more expensive,\n    // but in exchange the refund on every call to nonReentrant will be lower in\n    // amount. Since refunds are capped to a percentage of the total\n    // transaction's gas, it is best to keep them low in cases like this one, to\n    // increase the likelihood of the full refund coming into effect.\n    uint256 private constant _NOT_ENTERED = 1;\n    uint256 private constant _ENTERED = 2;\n\n    uint256 private _status;\n\n    constructor() {\n        _status = _NOT_ENTERED;\n    }\n\n    /**\n     * @dev Prevents a contract from calling itself, directly or indirectly.\n     * Calling a `nonReentrant` function from another `nonReentrant`\n     * function is not supported. It is possible to prevent this from happening\n     * by making the `nonReentrant` function external, and making it call a\n     * `private` function that does the actual work.\n     */\n    modifier nonReentrant() {\n        // On the first call to nonReentrant, _notEntered will be true\n        require(_status != _ENTERED, \"ReentrancyGuard: reentrant call\");\n\n        // Any calls to nonReentrant after this point will fail\n        _status = _ENTERED;\n\n        _;\n\n        // By storing the original value once again, a refund is triggered (see\n        // https://eips.ethereum.org/EIPS/eip-2200)\n        _status = _NOT_ENTERED;\n    }\n}\n"
    },
    "@openzeppelin/contracts/token/ERC20/IERC20.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC20/IERC20.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC20 standard as defined in the EIP.\n */\ninterface IERC20 {\n    /**\n     * @dev Returns the amount of tokens in existence.\n     */\n    function totalSupply() external view returns (uint256);\n\n    /**\n     * @dev Returns the amount of tokens owned by `account`.\n     */\n    function balanceOf(address account) external view returns (uint256);\n\n    /**\n     * @dev Moves `amount` tokens from the caller's account to `recipient`.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transfer(address recipient, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Returns the remaining number of tokens that `spender` will be\n     * allowed to spend on behalf of `owner` through {transferFrom}. This is\n     * zero by default.\n     *\n     * This value changes when {approve} or {transferFrom} are called.\n     */\n    function allowance(address owner, address spender) external view returns (uint256);\n\n    /**\n     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * IMPORTANT: Beware that changing an allowance with this method brings the risk\n     * that someone may use both the old and the new allowance by unfortunate\n     * transaction ordering. One possible solution to mitigate this race\n     * condition is to first reduce the spender's allowance to 0 and set the\n     * desired value afterwards:\n     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address spender, uint256 amount) external returns (bool);\n\n    /**\n     * @dev Moves `amount` tokens from `sender` to `recipient` using the\n     * allowance mechanism. `amount` is then deducted from the caller's\n     * allowance.\n     *\n     * Returns a boolean value indicating whether the operation succeeded.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(\n        address sender,\n        address recipient,\n        uint256 amount\n    ) external returns (bool);\n\n    /**\n     * @dev Emitted when `value` tokens are moved from one account (`from`) to\n     * another (`to`).\n     *\n     * Note that `value` may be zero.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 value);\n\n    /**\n     * @dev Emitted when the allowance of a `spender` for an `owner` is set by\n     * a call to {approve}. `value` is the new allowance.\n     */\n    event Approval(address indexed owner, address indexed spender, uint256 value);\n}\n"
    },
    "@openzeppelin/contracts/token/ERC721/IERC721.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (token/ERC721/IERC721.sol)\n\npragma solidity ^0.8.0;\n\nimport \"../../utils/introspection/IERC165.sol\";\n\n/**\n * @dev Required interface of an ERC721 compliant contract.\n */\ninterface IERC721 is IERC165 {\n    /**\n     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.\n     */\n    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.\n     */\n    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);\n\n    /**\n     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.\n     */\n    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);\n\n    /**\n     * @dev Returns the number of tokens in ``owner``'s account.\n     */\n    function balanceOf(address owner) external view returns (uint256 balance);\n\n    /**\n     * @dev Returns the owner of the `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function ownerOf(uint256 tokenId) external view returns (address owner);\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients\n     * are aware of the ERC721 protocol to prevent tokens from being forever locked.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Transfers `tokenId` token from `from` to `to`.\n     *\n     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     *\n     * Emits a {Transfer} event.\n     */\n    function transferFrom(\n        address from,\n        address to,\n        uint256 tokenId\n    ) external;\n\n    /**\n     * @dev Gives permission to `to` to transfer `tokenId` token to another account.\n     * The approval is cleared when the token is transferred.\n     *\n     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.\n     *\n     * Requirements:\n     *\n     * - The caller must own the token or be an approved operator.\n     * - `tokenId` must exist.\n     *\n     * Emits an {Approval} event.\n     */\n    function approve(address to, uint256 tokenId) external;\n\n    /**\n     * @dev Returns the account approved for `tokenId` token.\n     *\n     * Requirements:\n     *\n     * - `tokenId` must exist.\n     */\n    function getApproved(uint256 tokenId) external view returns (address operator);\n\n    /**\n     * @dev Approve or remove `operator` as an operator for the caller.\n     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.\n     *\n     * Requirements:\n     *\n     * - The `operator` cannot be the caller.\n     *\n     * Emits an {ApprovalForAll} event.\n     */\n    function setApprovalForAll(address operator, bool _approved) external;\n\n    /**\n     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.\n     *\n     * See {setApprovalForAll}\n     */\n    function isApprovedForAll(address owner, address operator) external view returns (bool);\n\n    /**\n     * @dev Safely transfers `tokenId` token from `from` to `to`.\n     *\n     * Requirements:\n     *\n     * - `from` cannot be the zero address.\n     * - `to` cannot be the zero address.\n     * - `tokenId` token must exist and be owned by `from`.\n     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.\n     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.\n     *\n     * Emits a {Transfer} event.\n     */\n    function safeTransferFrom(\n        address from,\n        address to,\n        uint256 tokenId,\n        bytes calldata data\n    ) external;\n}\n"
    },
    "@openzeppelin/contracts/utils/Address.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts (last updated v4.5.0-rc.0) (utils/Address.sol)\n\npragma solidity ^0.8.1;\n\n/**\n * @dev Collection of functions related to the address type\n */\nlibrary Address {\n    /**\n     * @dev Returns true if `account` is a contract.\n     *\n     * [IMPORTANT]\n     * ====\n     * It is unsafe to assume that an address for which this function returns\n     * false is an externally-owned account (EOA) and not a contract.\n     *\n     * Among others, `isContract` will return false for the following\n     * types of addresses:\n     *\n     *  - an externally-owned account\n     *  - a contract in construction\n     *  - an address where a contract will be created\n     *  - an address where a contract lived, but was destroyed\n     * ====\n     *\n     * [IMPORTANT]\n     * ====\n     * You shouldn't rely on `isContract` to protect against flash loan attacks!\n     *\n     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets\n     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract\n     * constructor.\n     * ====\n     */\n    function isContract(address account) internal view returns (bool) {\n        // This method relies on extcodesize/address.code.length, which returns 0\n        // for contracts in construction, since the code is only stored at the end\n        // of the constructor execution.\n\n        return account.code.length > 0;\n    }\n\n    /**\n     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to\n     * `recipient`, forwarding all available gas and reverting on errors.\n     *\n     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost\n     * of certain opcodes, possibly making contracts go over the 2300 gas limit\n     * imposed by `transfer`, making them unable to receive funds via\n     * `transfer`. {sendValue} removes this limitation.\n     *\n     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].\n     *\n     * IMPORTANT: because control is transferred to `recipient`, care must be\n     * taken to not create reentrancy vulnerabilities. Consider using\n     * {ReentrancyGuard} or the\n     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].\n     */\n    function sendValue(address payable recipient, uint256 amount) internal {\n        require(address(this).balance >= amount, \"Address: insufficient balance\");\n\n        (bool success, ) = recipient.call{value: amount}(\"\");\n        require(success, \"Address: unable to send value, recipient may have reverted\");\n    }\n\n    /**\n     * @dev Performs a Solidity function call using a low level `call`. A\n     * plain `call` is an unsafe replacement for a function call: use this\n     * function instead.\n     *\n     * If `target` reverts with a revert reason, it is bubbled up by this\n     * function (like regular Solidity function calls).\n     *\n     * Returns the raw returned data. To convert to the expected return value,\n     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].\n     *\n     * Requirements:\n     *\n     * - `target` must be a contract.\n     * - calling `target` with `data` must not revert.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionCall(target, data, \"Address: low-level call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with\n     * `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, 0, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but also transferring `value` wei to `target`.\n     *\n     * Requirements:\n     *\n     * - the calling contract must have an ETH balance of at least `value`.\n     * - the called Solidity function must be `payable`.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value\n    ) internal returns (bytes memory) {\n        return functionCallWithValue(target, data, value, \"Address: low-level call with value failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but\n     * with `errorMessage` as a fallback revert reason when `target` reverts.\n     *\n     * _Available since v3.1._\n     */\n    function functionCallWithValue(\n        address target,\n        bytes memory data,\n        uint256 value,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(address(this).balance >= value, \"Address: insufficient balance for call\");\n        require(isContract(target), \"Address: call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.call{value: value}(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {\n        return functionStaticCall(target, data, \"Address: low-level static call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a static call.\n     *\n     * _Available since v3.3._\n     */\n    function functionStaticCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal view returns (bytes memory) {\n        require(isContract(target), \"Address: static call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.staticcall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {\n        return functionDelegateCall(target, data, \"Address: low-level delegate call failed\");\n    }\n\n    /**\n     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],\n     * but performing a delegate call.\n     *\n     * _Available since v3.4._\n     */\n    function functionDelegateCall(\n        address target,\n        bytes memory data,\n        string memory errorMessage\n    ) internal returns (bytes memory) {\n        require(isContract(target), \"Address: delegate call to non-contract\");\n\n        (bool success, bytes memory returndata) = target.delegatecall(data);\n        return verifyCallResult(success, returndata, errorMessage);\n    }\n\n    /**\n     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the\n     * revert reason using the provided one.\n     *\n     * _Available since v4.3._\n     */\n    function verifyCallResult(\n        bool success,\n        bytes memory returndata,\n        string memory errorMessage\n    ) internal pure returns (bytes memory) {\n        if (success) {\n            return returndata;\n        } else {\n            // Look for revert reason and bubble it up if present\n            if (returndata.length > 0) {\n                // The easiest way to bubble the revert reason is using memory via assembly\n\n                assembly {\n                    let returndata_size := mload(returndata)\n                    revert(add(32, returndata), returndata_size)\n                }\n            } else {\n                revert(errorMessage);\n            }\n        }\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/Context.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Provides information about the current execution context, including the\n * sender of the transaction and its data. While these are generally available\n * via msg.sender and msg.data, they should not be accessed in such a direct\n * manner, since when dealing with meta-transactions the account sending and\n * paying for execution may not be the actual sender (as far as an application\n * is concerned).\n *\n * This contract is only required for intermediate, library-like contracts.\n */\nabstract contract Context {\n    function _msgSender() internal view virtual returns (address) {\n        return msg.sender;\n    }\n\n    function _msgData() internal view virtual returns (bytes calldata) {\n        return msg.data;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/ERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)\n\npragma solidity ^0.8.0;\n\nimport \"./IERC165.sol\";\n\n/**\n * @dev Implementation of the {IERC165} interface.\n *\n * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check\n * for the additional interface id that will be supported. For example:\n *\n * ```solidity\n * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);\n * }\n * ```\n *\n * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.\n */\nabstract contract ERC165 is IERC165 {\n    /**\n     * @dev See {IERC165-supportsInterface}.\n     */\n    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {\n        return interfaceId == type(IERC165).interfaceId;\n    }\n}\n"
    },
    "@openzeppelin/contracts/utils/introspection/IERC165.sol": {
      "content": "// SPDX-License-Identifier: MIT\n// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)\n\npragma solidity ^0.8.0;\n\n/**\n * @dev Interface of the ERC165 standard, as defined in the\n * https://eips.ethereum.org/EIPS/eip-165[EIP].\n *\n * Implementers can declare support of contract interfaces, which can then be\n * queried by others ({ERC165Checker}).\n *\n * For an implementation, see {ERC165}.\n */\ninterface IERC165 {\n    /**\n     * @dev Returns true if this contract implements the interface defined by\n     * `interfaceId`. See the corresponding\n     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]\n     * to learn more about how these ids are created.\n     *\n     * This function call must use less than 30 000 gas.\n     */\n    function supportsInterface(bytes4 interfaceId) external view returns (bool);\n}\n"
    },
    "@paulrberg/contracts/math/PRBMath.sol": {
      "content": "// SPDX-License-Identifier: Unlicense\npragma solidity >=0.8.4;\n\nimport \"prb-math/contracts/PRBMath.sol\";\n"
    },
    "contracts/JBETHERC20ProjectPayer.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/ERC165.sol';\nimport '@openzeppelin/contracts/access/Ownable.sol';\nimport '@openzeppelin/contracts/token/ERC20/IERC20.sol';\nimport './interfaces/IJBProjectPayer.sol';\nimport './libraries/JBTokens.sol';\n\n/** \n  @notice \n  Sends ETH or ERC20's to a project treasury as it receives direct payments or has it's functions called.\n\n  @dev\n  Inherit from this contract or borrow from its logic to forward ETH or ERC20's to project treasuries from within other contracts.\n\n  @dev\n  Adheres to -\n  IJBProjectPayer:  General interface for the methods in this contract that interact with the blockchain's state according to the protocol's rules.\n\n  @dev\n  Inherits from -\n  Ownable: Includes convenience functionality for checking a message sender's permissions before executing certain transactions.\n  ERC165: Introspection on interface adherance. \n*/\ncontract JBETHERC20ProjectPayer is IJBProjectPayer, Ownable, ERC165 {\n  //*********************************************************************//\n  // -------------------------- custom errors -------------------------- //\n  //*********************************************************************//\n  error INCORRECT_DECIMAL_AMOUNT();\n  error NO_MSG_VALUE_ALLOWED();\n  error TERMINAL_NOT_FOUND();\n\n  //*********************************************************************//\n  // ---------------- public immutable stored properties --------------- //\n  //*********************************************************************//\n\n  /**\n    @notice \n    A contract storing directories of terminals and controllers for each project.\n  */\n  IJBDirectory public immutable override directory;\n\n  //*********************************************************************//\n  // --------------------- public stored properties -------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    The ID of the project that should be used to forward this contract's received payments.\n  */\n  uint256 public override defaultProjectId;\n\n  /** \n    @notice \n    The beneficiary that should be used in the payment made when this contract receives payments.\n  */\n  address payable public override defaultBeneficiary;\n\n  /** \n    @notice \n    A flag indicating whether issued tokens should be automatically claimed into the beneficiary's wallet. Leaving tokens unclaimed saves gas.\n  */\n  bool public override defaultPreferClaimedTokens;\n\n  /** \n    @notice \n    The memo that should be used in the payment made when this contract receives payments.\n  */\n  string public override defaultMemo;\n\n  /** \n    @notice \n    The metadata that should be used in the payment made when this contract receives payments.\n  */\n  bytes public override defaultMetadata;\n\n  /**\n    @notice \n    A flag indicating if received payments should call the `pay` function or the `addToBalance` function of a project.\n  */\n  bool public override defaultPreferAddToBalance;\n\n  //*********************************************************************//\n  // ------------------------- public views -------------------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    Indicates if this contract adheres to the specified interface.\n\n    @dev \n    See {IERC165-supportsInterface}.\n\n    @param _interfaceId The ID of the interface to check for adherance to.\n  */\n  function supportsInterface(bytes4 _interfaceId)\n    public\n    view\n    virtual\n    override(ERC165, IERC165)\n    returns (bool)\n  {\n    return\n      _interfaceId == type(IJBProjectPayer).interfaceId || super.supportsInterface(_interfaceId);\n  }\n\n  //*********************************************************************//\n  // -------------------------- constructor ---------------------------- //\n  //*********************************************************************//\n\n  /** \n    @param _defaultProjectId The ID of the project whose treasury should be forwarded this contract's received payments.\n    @param _defaultBeneficiary The address that'll receive the project's tokens. \n    @param _defaultPreferClaimedTokens A flag indicating whether issued tokens should be automatically claimed into the beneficiary's wallet. \n    @param _defaultMemo A memo to pass along to the emitted event, and passed along the the funding cycle's data source and delegate.  A data source can alter the memo before emitting in the event and forwarding to the delegate.\n    @param _defaultMetadata Bytes to send along to the project's data source and delegate, if provided.\n    @param _defaultPreferAddToBalance A flag indicating if received payments should call the `pay` function or the `addToBalance` function of a project.\n    @param _directory A contract storing directories of terminals and controllers for each project.\n    @param _owner The address that will own the contract.\n  */\n  constructor(\n    uint256 _defaultProjectId,\n    address payable _defaultBeneficiary,\n    bool _defaultPreferClaimedTokens,\n    string memory _defaultMemo,\n    bytes memory _defaultMetadata,\n    bool _defaultPreferAddToBalance,\n    IJBDirectory _directory,\n    address _owner\n  ) {\n    defaultProjectId = _defaultProjectId;\n    defaultBeneficiary = _defaultBeneficiary;\n    defaultPreferClaimedTokens = _defaultPreferClaimedTokens;\n    defaultMemo = _defaultMemo;\n    defaultMetadata = _defaultMetadata;\n    defaultPreferAddToBalance = _defaultPreferAddToBalance;\n    directory = _directory;\n\n    _transferOwnership(_owner);\n  }\n\n  //*********************************************************************//\n  // ------------------------- default receive ------------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice\n    Received funds are paid to the default project ID using the stored default properties.\n\n    @dev\n    Use the `addToBalance` function if there's a preference to do so. Otherwise use `pay`.\n\n    @dev\n    This function is called automatically when the contract receives an ETH payment.\n  */\n  receive() external payable virtual override {\n    if (defaultPreferAddToBalance)\n      _addToBalanceOf(\n        defaultProjectId,\n        JBTokens.ETH,\n        address(this).balance,\n        18, // balance is a fixed point number with 18 decimals.\n        defaultMemo,\n        defaultMetadata\n      );\n    else\n      _pay(\n        defaultProjectId,\n        JBTokens.ETH,\n        address(this).balance,\n        18, // balance is a fixed point number with 18 decimals.\n        defaultBeneficiary == address(0) ? msg.sender : defaultBeneficiary,\n        0, // Can't determine expectation of returned tokens ahead of time.\n        defaultPreferClaimedTokens,\n        defaultMemo,\n        defaultMetadata\n      );\n  }\n\n  //*********************************************************************//\n  // ---------------------- external transactions ---------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    Sets the default values that determine how to interact with a protocol treasury when this contract receives ETH directly.\n\n    @param _projectId The ID of the project whose treasury should be forwarded this contract's received payments.\n    @param _beneficiary The address that'll receive the project's tokens. \n    @param _preferClaimedTokens A flag indicating whether issued tokens should be automatically claimed into the beneficiary's wallet. \n    @param _memo The memo that'll be used. \n    @param _metadata The metadata that'll be sent. \n    @param _defaultPreferAddToBalance A flag indicating if received payments should call the `pay` function or the `addToBalance` function of a project.\n  */\n  function setDefaultValues(\n    uint256 _projectId,\n    address payable _beneficiary,\n    bool _preferClaimedTokens,\n    string memory _memo,\n    bytes memory _metadata,\n    bool _defaultPreferAddToBalance\n  ) external virtual override onlyOwner {\n    // Set the default project ID if it has changed.\n    if (_projectId != defaultProjectId) defaultProjectId = _projectId;\n\n    // Set the default beneficiary if it has changed.\n    if (_beneficiary != defaultBeneficiary) defaultBeneficiary = _beneficiary;\n\n    // Set the default claimed token preference if it has changed.\n    if (_preferClaimedTokens != defaultPreferClaimedTokens)\n      defaultPreferClaimedTokens = _preferClaimedTokens;\n\n    // Set the default memo if it has changed.\n    if (keccak256(abi.encodePacked(_memo)) != keccak256(abi.encodePacked(defaultMemo)))\n      defaultMemo = _memo;\n\n    // Set the default metadata if it has changed.\n    if (keccak256(abi.encodePacked(_metadata)) != keccak256(abi.encodePacked(defaultMetadata)))\n      defaultMetadata = _metadata;\n\n    // Set the add to balance preference if it has changed.\n    if (_defaultPreferAddToBalance != defaultPreferAddToBalance)\n      defaultPreferAddToBalance = _defaultPreferAddToBalance;\n\n    emit SetDefaultValues(\n      _projectId,\n      _beneficiary,\n      _preferClaimedTokens,\n      _memo,\n      _metadata,\n      _defaultPreferAddToBalance,\n      msg.sender\n    );\n  }\n\n  //*********************************************************************//\n  // ----------------------- public transactions ----------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    Make a payment to the specified project.\n\n    @param _projectId The ID of the project that is being paid.\n    @param _token The token being paid in.\n    @param _amount The amount of tokens being paid, as a fixed point number. If the token is ETH, this is ignored and msg.value is used in its place.\n    @param _decimals The number of decimals in the `_amount` fixed point number. If the token is ETH, this is ignored and 18 is used in its place, which corresponds to the amount of decimals expected in msg.value.\n    @param _beneficiary The address who will receive tokens from the payment.\n    @param _minReturnedTokens The minimum number of project tokens expected in return, as a fixed point number with 18 decimals.\n    @param _preferClaimedTokens A flag indicating whether the request prefers to mint project tokens into the beneficiaries wallet rather than leaving them unclaimed. This is only possible if the project has an attached token contract. Leaving them unclaimed saves gas.\n    @param _memo A memo to pass along to the emitted event, and passed along the the funding cycle's data source and delegate. A data source can alter the memo before emitting in the event and forwarding to the delegate.\n    @param _metadata Bytes to send along to the data source, delegate, and emitted event, if provided.\n  */\n  function pay(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    address _beneficiary,\n    uint256 _minReturnedTokens,\n    bool _preferClaimedTokens,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) public payable virtual override {\n    // ETH shouldn't be sent if the token isn't ETH.\n    if (address(_token) != JBTokens.ETH) {\n      if (msg.value > 0) revert NO_MSG_VALUE_ALLOWED();\n\n      // Transfer tokens to this contract from the msg sender.\n      IERC20(_token).transferFrom(msg.sender, address(this), _amount);\n    } else {\n      // If ETH is being paid, set the amount to the message value, and decimals to 18.\n      _amount = msg.value;\n      _decimals = 18;\n    }\n\n    _pay(\n      _projectId,\n      _token,\n      _amount,\n      _decimals,\n      _beneficiary,\n      _minReturnedTokens,\n      _preferClaimedTokens,\n      _memo,\n      _metadata\n    );\n  }\n\n  /** \n    @notice \n    Add to the balance of the specified project.\n\n    @param _projectId The ID of the project that is being paid.\n    @param _token The token being paid in.\n    @param _amount The amount of tokens being paid, as a fixed point number. If the token is ETH, this is ignored and msg.value is used in its place.\n    @param _decimals The number of decimals in the `_amount` fixed point number. If the token is ETH, this is ignored and 18 is used in its place, which corresponds to the amount of decimals expected in msg.value.\n    @param _memo A memo to pass along to the emitted event.\n    @param _metadata Extra data to pass along to the terminal.\n  */\n  function addToBalanceOf(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) public payable virtual override {\n    // ETH shouldn't be sent if the token isn't ETH.\n    if (address(_token) != JBTokens.ETH) {\n      if (msg.value > 0) revert NO_MSG_VALUE_ALLOWED();\n\n      // Transfer tokens to this contract from the msg sender.\n      IERC20(_token).transferFrom(msg.sender, address(this), _amount);\n    } else {\n      // If ETH is being paid, set the amount to the message value, and decimals to 18.\n      _amount = msg.value;\n      _decimals = 18;\n    }\n\n    _addToBalanceOf(_projectId, _token, _amount, _decimals, _memo, _metadata);\n  }\n\n  //*********************************************************************//\n  // ---------------------- internal transactions ---------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    Make a payment to the specified project.\n\n    @param _projectId The ID of the project that is being paid.\n    @param _token The token being paid in.\n    @param _amount The amount of tokens being paid, as a fixed point number. \n    @param _decimals The number of decimals in the `_amount` fixed point number. \n    @param _beneficiary The address who will receive tokens from the payment.\n    @param _minReturnedTokens The minimum number of project tokens expected in return, as a fixed point number with 18 decimals.\n    @param _preferClaimedTokens A flag indicating whether the request prefers to mint project tokens into the beneficiaries wallet rather than leaving them unclaimed. This is only possible if the project has an attached token contract. Leaving them unclaimed saves gas.\n    @param _memo A memo to pass along to the emitted event, and passed along the the funding cycle's data source and delegate.  A data source can alter the memo before emitting in the event and forwarding to the delegate.\n    @param _metadata Bytes to send along to the data source and delegate, if provided.\n  */\n  function _pay(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    address _beneficiary,\n    uint256 _minReturnedTokens,\n    bool _preferClaimedTokens,\n    string memory _memo,\n    bytes memory _metadata\n  ) internal virtual {\n    // Find the terminal for the specified project.\n    IJBPaymentTerminal _terminal = directory.primaryTerminalOf(_projectId, _token);\n\n    // There must be a terminal.\n    if (_terminal == IJBPaymentTerminal(address(0))) revert TERMINAL_NOT_FOUND();\n\n    // The amount's decimals must match the terminal's expected decimals.\n    if (_terminal.decimalsForToken(_token) != _decimals) revert INCORRECT_DECIMAL_AMOUNT();\n\n    // Approve the `_amount` of tokens from the destination terminal to transfer tokens from this contract.\n    if (_token != JBTokens.ETH) IERC20(_token).approve(address(_terminal), _amount);\n\n    // If the token is ETH, send it in msg.value.\n    uint256 _payableValue = _token == JBTokens.ETH ? _amount : 0;\n\n    // Send funds to the terminal.\n    // If the token is ETH, send it in msg.value.\n    _terminal.pay{value: _payableValue}(\n      _projectId,\n      _amount, // ignored if the token is JBTokens.ETH.\n      _token,\n      _beneficiary != address(0) ? _beneficiary : msg.sender,\n      _minReturnedTokens,\n      _preferClaimedTokens,\n      _memo,\n      _metadata\n    );\n  }\n\n  /** \n    @notice \n    Add to the balance of the specified project.\n\n    @param _projectId The ID of the project that is being paid.\n    @param _token The token being paid in.\n    @param _amount The amount of tokens being paid, as a fixed point number. If the token is ETH, this is ignored and msg.value is used in its place.\n    @param _decimals The number of decimals in the `_amount` fixed point number. If the token is ETH, this is ignored and 18 is used in its place, which corresponds to the amount of decimals expected in msg.value.\n    @param _memo A memo to pass along to the emitted event.\n    @param _metadata Extra data to pass along to the terminal.\n  */\n  function _addToBalanceOf(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    string memory _memo,\n    bytes memory _metadata\n  ) internal virtual {\n    // Find the terminal for the specified project.\n    IJBPaymentTerminal _terminal = directory.primaryTerminalOf(_projectId, _token);\n\n    // There must be a terminal.\n    if (_terminal == IJBPaymentTerminal(address(0))) revert TERMINAL_NOT_FOUND();\n\n    // The amount's decimals must match the terminal's expected decimals.\n    if (_terminal.decimalsForToken(_token) != _decimals) revert INCORRECT_DECIMAL_AMOUNT();\n\n    // Approve the `_amount` of tokens from the destination terminal to transfer tokens from this contract.\n    if (_token != JBTokens.ETH) IERC20(_token).approve(address(_terminal), _amount);\n\n    // If the token is ETH, send it in msg.value.\n    uint256 _payableValue = _token == JBTokens.ETH ? _amount : 0;\n\n    // Add to balance so tokens don't get issued.\n    _terminal.addToBalanceOf{value: _payableValue}(_projectId, _amount, _token, _memo, _metadata);\n  }\n}\n"
    },
    "contracts/JBETHERC20SplitsPayer.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/security/ReentrancyGuard.sol';\nimport '@openzeppelin/contracts/utils/Address.sol';\nimport '@paulrberg/contracts/math/PRBMath.sol';\nimport './interfaces/IJBSplitsPayer.sol';\nimport './interfaces/IJBSplitsStore.sol';\nimport './libraries/JBConstants.sol';\nimport './JBETHERC20ProjectPayer.sol';\n\n/** \n  @notice \n  Sends ETH or ERC20's to a group of splits as it receives direct payments or has its functions called.\n\n  @dev\n  Inherit from this contract or borrow from its logic to forward ETH or ERC20's to a group of splits from within other contracts.\n\n  @dev\n  Adheres to -\n  IJBSplitsPayer:  General interface for the methods in this contract that interact with the blockchain's state according to the protocol's rules.\n\n  @dev\n  Inherits from -\n  JBETHERC20ProjectPayer: Sends ETH or ERC20's to a project treasury as it receives direct payments or has it's functions called.\n  ReentrancyGuard: Contract module that helps prevent reentrant calls to a function.\n*/\ncontract JBETHERC20SplitsPayer is IJBSplitsPayer, JBETHERC20ProjectPayer, ReentrancyGuard {\n  //*********************************************************************//\n  // ---------------- public immutable stored properties --------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    The contract that stores splits for each project.\n  */\n  IJBSplitsStore public immutable override splitsStore;\n\n  //*********************************************************************//\n  // --------------------- public stored properties -------------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    The ID of project for which the default splits are stored. \n  */\n  uint256 public override defaultSplitsProjectId;\n\n  /**\n    @notice\n    The domain within which the default splits are stored. \n  */\n  uint256 public override defaultSplitsDomain;\n\n  /**\n    @notice\n    The group within which the default splits are stored. \n  */\n  uint256 public override defaultSplitsGroup;\n\n  //*********************************************************************//\n  // -------------------------- public views --------------------------- //\n  //*********************************************************************//\n\n  /**\n    @notice\n    Indicates if this contract adheres to the specified interface.\n\n    @dev \n    See {IERC165-supportsInterface}.\n\n    @param _interfaceId The ID of the interface to check for adherance to.\n\n    @return A flag indicating if this contract adheres to the specified interface.\n  */\n  function supportsInterface(bytes4 _interfaceId)\n    public\n    view\n    virtual\n    override(JBETHERC20ProjectPayer, IERC165)\n    returns (bool)\n  {\n    return\n      _interfaceId == type(IJBSplitsPayer).interfaceId || super.supportsInterface(_interfaceId);\n  }\n\n  //*********************************************************************//\n  // -------------------------- constructor ---------------------------- //\n  //*********************************************************************//\n\n  /** \n    @param _defaultSplitsProjectId The ID of project for which the default splits are stored.\n    @param _defaultSplitsDomain The splits domain to payout when this contract receives direct payments.\n    @param _defaultSplitsGroup The splits group to payout when this contract receives direct payments.\n    @param _splitsStore A contract that stores splits for each project.\n    @param _defaultProjectId The ID of the project whose treasury should be forwarded the splits payer contract's received payment leftovers after distributing to the default splits group.\n    @param _defaultBeneficiary The address that'll receive the project's tokens. \n    @param _defaultPreferClaimedTokens A flag indicating whether issued tokens should be automatically claimed into the beneficiary's wallet. \n    @param _defaultMemo A memo to pass along to the emitted event, and passed along the the funding cycle's data source and delegate.  A data source can alter the memo before emitting in the event and forwarding to the delegate.\n    @param _defaultMetadata Bytes to send along to the project's data source and delegate, if provided.\n    @param _preferAddToBalance  A flag indicating if received payments should call the `pay` function or the `addToBalance` function of a project.\n    @param _owner The address that will own the contract.\n  */\n  constructor(\n    uint256 _defaultSplitsProjectId,\n    uint256 _defaultSplitsDomain,\n    uint256 _defaultSplitsGroup,\n    IJBSplitsStore _splitsStore,\n    uint256 _defaultProjectId,\n    address payable _defaultBeneficiary,\n    bool _defaultPreferClaimedTokens,\n    string memory _defaultMemo,\n    bytes memory _defaultMetadata,\n    bool _preferAddToBalance,\n    address _owner\n  )\n    JBETHERC20ProjectPayer(\n      _defaultProjectId,\n      _defaultBeneficiary,\n      _defaultPreferClaimedTokens,\n      _defaultMemo,\n      _defaultMetadata,\n      _preferAddToBalance,\n      _splitsStore.directory(),\n      _owner\n    )\n  {\n    defaultSplitsProjectId = _defaultSplitsProjectId;\n    defaultSplitsDomain = _defaultSplitsDomain;\n    defaultSplitsGroup = _defaultSplitsGroup;\n    splitsStore = _splitsStore;\n  }\n\n  //*********************************************************************//\n  // ------------------------- default receive ------------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice\n    Received funds are paid to the default split group using the stored default properties.\n\n    @dev\n    This function is called automatically when the contract receives an ETH payment.\n  */\n  receive() external payable virtual override nonReentrant {\n    // Pay the splits and get a reference to the amount leftover.\n    uint256 _leftoverAmount = _payToSplits(\n      defaultSplitsProjectId,\n      defaultSplitsDomain,\n      defaultSplitsGroup,\n      JBTokens.ETH,\n      address(this).balance,\n      18 // decimals.\n    );\n\n    // If there is no leftover amount, nothing left to pay.\n    if (_leftoverAmount == 0) return;\n\n    // If there's a default project ID, try to pay it.\n    if (defaultProjectId != 0)\n      if (defaultPreferAddToBalance)\n        // Pay the project by adding to its balance if prefered.\n        _addToBalanceOf(\n          defaultProjectId,\n          JBTokens.ETH,\n          _leftoverAmount,\n          18, // decimals.\n          defaultMemo,\n          defaultMetadata\n        );\n        // Otherwise, issue a payment to the project.\n      else\n        _pay(\n          defaultProjectId,\n          JBTokens.ETH,\n          _leftoverAmount,\n          18, // decimals.\n          defaultBeneficiary != address(0) ? defaultBeneficiary : msg.sender,\n          0, // min returned tokens.\n          defaultPreferClaimedTokens,\n          defaultMemo,\n          defaultMetadata\n        );\n    // If no project was specified, send the funds directly to the beneficiary or the msg.sender.\n    else\n      Address.sendValue(\n        defaultBeneficiary != address(0) ? payable(defaultBeneficiary) : payable(msg.sender),\n        _leftoverAmount\n      );\n  }\n\n  //*********************************************************************//\n  // ---------------------- external transactions ---------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice\n    Sets the location of the splits that payments this contract receives will be split between.\n\n    @param _projectId The ID of project for which the default splits are stored. \n    @param _domain The domain within which the default splits are stored. \n    @param _group The group within which the default splits are stored. \n  */\n  function setDefaultSplits(\n    uint256 _projectId,\n    uint256 _domain,\n    uint256 _group\n  ) external virtual override onlyOwner {\n    // Set the default splits project ID if it's changing.\n    if (_projectId != defaultSplitsProjectId) defaultSplitsProjectId = _projectId;\n\n    // Set the default splits domain if it's changing.\n    if (_domain != defaultSplitsDomain) defaultSplitsDomain = _domain;\n\n    // Set the default splits group if it's changing.\n    if (_group != defaultSplitsGroup) defaultSplitsGroup = _group;\n\n    emit SetDefaultSplits(_projectId, _domain, _group, msg.sender);\n  }\n\n  //*********************************************************************//\n  // ----------------------- public transactions ----------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    Make a payment to the specified project after first splitting the amount among the stored default splits.\n\n    @param _projectId The ID of the project that is being paid after.\n    @param _token The token being paid in.\n    @param _amount The amount of tokens being paid, as a fixed point number. If the token is ETH, this is ignored and msg.value is used in its place.\n    @param _decimals The number of decimals in the `_amount` fixed point number. If the token is ETH, this is ignored and 18 is used in its place, which corresponds to the amount of decimals expected in msg.value.\n    @param _beneficiary The address who will receive tokens from the payment made with leftover funds.\n    @param _minReturnedTokens The minimum number of project tokens expected in return, as a fixed point number with 18 decimals.\n    @param _preferClaimedTokens A flag indicating whether the request prefers to mint project tokens into the beneficiaries wallet rather than leaving them unclaimed. This is only possible if the project has an attached token contract. Leaving them unclaimed saves gas.\n    @param _memo A memo to pass along to the emitted event, and passed along the the funding cycle's data source and delegate. A data source can alter the memo before emitting in the event and forwarding to the delegate.\n    @param _metadata Bytes to send along to the data source, delegate, and emitted event, if provided.\n  */\n  function pay(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    address _beneficiary,\n    uint256 _minReturnedTokens,\n    bool _preferClaimedTokens,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) public payable virtual override nonReentrant {\n    // ETH shouldn't be sent if the token isn't ETH.\n    if (address(_token) != JBTokens.ETH) {\n      if (msg.value > 0) revert NO_MSG_VALUE_ALLOWED();\n\n      // Transfer tokens to this contract from the msg sender.\n      IERC20(_token).transferFrom(msg.sender, address(this), _amount);\n    } else {\n      // If ETH is being paid, set the amount to the message value, and decimals to 18.\n      _amount = msg.value;\n      _decimals = 18;\n    }\n\n    // Pay the splits and get a reference to the amount leftover.\n    uint256 _leftoverAmount = _payToSplits(\n      defaultSplitsProjectId,\n      defaultSplitsDomain,\n      defaultSplitsGroup,\n      _token,\n      _amount,\n      _decimals\n    );\n\n    // Pay any leftover amount.\n    if (_leftoverAmount > 0) {\n      // If there's a default project ID, try to pay it.\n      if (_projectId != 0) {\n        _pay(\n          _projectId,\n          _token,\n          _leftoverAmount,\n          _decimals,\n          _beneficiary != address(0) ? _beneficiary : msg.sender,\n          _minReturnedTokens,\n          _preferClaimedTokens,\n          _memo,\n          _metadata\n        );\n      }\n      // If no project was specified, send the funds directly to the beneficiary or the msg.sender.\n      else {\n        // Transfer the ETH.\n        if (_token == JBTokens.ETH)\n          Address.sendValue(\n            // If there's a beneficiary, send the funds directly to the beneficiary. Otherwise send to the msg.sender.\n            _beneficiary != address(0) ? payable(_beneficiary) : payable(msg.sender),\n            _leftoverAmount\n          );\n          // Or, transfer the ERC20.\n        else\n          IERC20(_token).transfer(\n            // If there's a beneficiary, send the funds directly to the beneficiary. Otherwise send to the msg.sender.\n            _beneficiary != address(0) ? _beneficiary : msg.sender,\n            _leftoverAmount\n          );\n      }\n    }\n\n    emit Pay(\n      _projectId,\n      _beneficiary != address(0) ? defaultBeneficiary : msg.sender,\n      _token,\n      _amount,\n      _decimals,\n      _leftoverAmount,\n      _minReturnedTokens,\n      _preferClaimedTokens,\n      _memo,\n      _metadata,\n      msg.sender\n    );\n  }\n\n  /** \n    @notice \n    Add to the balance of the specified project after first splitting the amount among the stored default splits.\n\n    @param _projectId The ID of the project that is being paid after.\n    @param _token The token being paid in.\n    @param _amount The amount of tokens being paid, as a fixed point number. If the token is ETH, this is ignored and msg.value is used in its place.\n    @param _decimals The number of decimals in the `_amount` fixed point number. If the token is ETH, this is ignored and 18 is used in its place, which corresponds to the amount of decimals expected in msg.value.\n    @param _memo A memo to pass along to the emitted event.  \n    @param _metadata Extra data to pass along to the terminal.\n  */\n  function addToBalanceOf(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) public payable virtual override nonReentrant {\n    // ETH shouldn't be sent if this terminal's token isn't ETH.\n    if (address(_token) != JBTokens.ETH) {\n      if (msg.value > 0) revert NO_MSG_VALUE_ALLOWED();\n\n      // Transfer tokens to this contract from the msg sender.\n      IERC20(_token).transferFrom(msg.sender, address(this), _amount);\n    } else {\n      // If ETH is being paid, set the amount to the message value, and decimals to 18.\n      _amount = msg.value;\n      _decimals = 18;\n    }\n\n    // Pay the splits and get a reference to the amount leftover.\n    uint256 _leftoverAmount = _payToSplits(\n      defaultSplitsProjectId,\n      defaultSplitsDomain,\n      defaultSplitsGroup,\n      _token,\n      _amount,\n      _decimals\n    );\n\n    // Distribute any leftover amount.\n    if (_leftoverAmount > 0) {\n      // If there's a default project ID, try to add to its balance.\n      if (_projectId != 0)\n        // Add to the project's balance.\n        _addToBalanceOf(_projectId, _token, _leftoverAmount, _decimals, _memo, _metadata);\n\n        // Otherwise, send a payment to the beneficiary.\n      else {\n        // Transfer the ETH.\n        if (_token == JBTokens.ETH)\n          Address.sendValue(\n            // If there's a default beneficiary, send the funds directly to the beneficiary. Otherwise send to the msg.sender.\n            defaultBeneficiary != address(0) ? defaultBeneficiary : payable(msg.sender),\n            _leftoverAmount\n          );\n          // Or, transfer the ERC20.\n        else\n          IERC20(_token).transfer(\n            // If there's a default beneficiary, send the funds directly to the beneficiary. Otherwise send to the msg.sender.\n            defaultBeneficiary != address(0) ? defaultBeneficiary : msg.sender,\n            _leftoverAmount\n          );\n      }\n    }\n\n    emit AddToBalance(\n      _projectId,\n      defaultBeneficiary != address(0) ? defaultBeneficiary : msg.sender,\n      _token,\n      _amount,\n      _decimals,\n      _leftoverAmount,\n      _memo,\n      _metadata,\n      msg.sender\n    );\n  }\n\n  //*********************************************************************//\n  // ---------------------- internal transactions ---------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    Split an amount between all splits.\n\n    @param _splitsProjectId The ID of the project to which the splits belong.\n    @param _splitsDomain The splits domain to which the group belongs.\n    @param _splitsGroup The splits group to pay.\n    @param _token The token the amonut being split is in.\n    @param _amount The amount of tokens being split, as a fixed point number. If the `_token` is ETH, this is ignored and msg.value is used in its place.\n    @param _decimals The number of decimals in the `_amount` fixed point number. \n\n    @return leftoverAmount The amount leftover after all splits were paid.\n  */\n  function _payToSplits(\n    uint256 _splitsProjectId,\n    uint256 _splitsDomain,\n    uint256 _splitsGroup,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals\n  ) internal virtual returns (uint256 leftoverAmount) {\n    // Get a reference to the splits.\n    JBSplit[] memory _splits = splitsStore.splitsOf(_splitsProjectId, _splitsDomain, _splitsGroup);\n\n    // Set the leftover amount to the initial balance.\n    leftoverAmount = _amount;\n\n    // Settle between all splits.\n    for (uint256 i = 0; i < _splits.length; i++) {\n      // Get a reference to the split being iterated on.\n      JBSplit memory _split = _splits[i];\n\n      // The amount to send towards the split.\n      uint256 _splitAmount = PRBMath.mulDiv(\n        _amount,\n        _split.percent,\n        JBConstants.SPLITS_TOTAL_PERCENT\n      );\n\n      if (_splitAmount > 0) {\n        // Transfer tokens to the split.\n        // If there's an allocator set, transfer to its `allocate` function.\n        if (_split.allocator != IJBSplitAllocator(address(0))) {\n          // Create the data to send to the allocator.\n          JBSplitAllocationData memory _data = JBSplitAllocationData(\n            _token,\n            _splitAmount,\n            _decimals,\n            defaultProjectId,\n            0,\n            _split\n          );\n\n          // Approve the `_amount` of tokens for the split allocator to transfer tokens from this contract.\n          if (_token != JBTokens.ETH)\n            IERC20(_token).approve(address(_split.allocator), _splitAmount);\n\n          // If the token is ETH, send it in msg.value.\n          uint256 _payableValue = _token == JBTokens.ETH ? _splitAmount : 0;\n\n          // Trigger the allocator's `allocate` function.\n          _split.allocator.allocate{value: _payableValue}(_data);\n\n          // Otherwise, if a project is specified, make a payment to it.\n        } else if (_split.projectId != 0) {\n          if (_split.preferAddToBalance)\n            _addToBalanceOf(\n              _split.projectId,\n              _token,\n              _splitAmount,\n              _decimals,\n              defaultMemo,\n              defaultMetadata\n            );\n          else\n            _pay(\n              _split.projectId,\n              _token,\n              _splitAmount,\n              _decimals,\n              _split.beneficiary != address(0) ? _split.beneficiary : msg.sender,\n              0,\n              _split.preferClaimed,\n              defaultMemo,\n              defaultMetadata\n            );\n        } else {\n          // Transfer the ETH.\n          if (_token == JBTokens.ETH)\n            Address.sendValue(\n              // Get a reference to the address receiving the tokens. If there's a beneficiary, send the funds directly to the beneficiary. Otherwise send to the msg.sender.\n              _split.beneficiary != address(0) ? _split.beneficiary : payable(msg.sender),\n              _splitAmount\n            );\n            // Or, transfer the ERC20.\n          else {\n            IERC20(_token).transfer(\n              // Get a reference to the address receiving the tokens. If there's a beneficiary, send the funds directly to the beneficiary. Otherwise send to the msg.sender.\n              _split.beneficiary != address(0) ? _split.beneficiary : msg.sender,\n              _splitAmount\n            );\n          }\n        }\n\n        // Subtract from the amount to be sent to the beneficiary.\n        leftoverAmount = leftoverAmount - _splitAmount;\n      }\n\n      emit DistributeToSplit(\n        _splitsProjectId,\n        _splitsDomain,\n        _splitsGroup,\n        _split,\n        _splitAmount,\n        msg.sender\n      );\n    }\n  }\n}\n"
    },
    "contracts/JBETHERC20SplitsPayerDeployer.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './interfaces/IJBETHERC20SplitsPayerDeployer.sol';\nimport './JBETHERC20SplitsPayer.sol';\n\n/** \n  @notice \n  Deploys splits payer contracts.\n\n  @dev\n  Adheres to -\n  IJBETHERC20SplitsPayerDeployer:  General interface for the methods in this contract that interact with the blockchain's state according to the protocol's rules.\n*/\ncontract JBETHERC20SplitsPayerDeployer is IJBETHERC20SplitsPayerDeployer {\n  //*********************************************************************//\n  // ---------------------- external transactions ---------------------- //\n  //*********************************************************************//\n\n  /** \n    @notice \n    Allows anyone to deploy a new splits payer contract.\n\n    @param _defaultSplitsProjectId The ID of project for which the default splits are stored.\n    @param _defaultSplitsDomain The splits domain to payout when this contract receives direct payments.\n    @param _defaultSplitsGroup The splits group to payout when this contract receives direct payments.\n    @param _splitsStore A contract that stores splits for each project.\n    @param _defaultProjectId The ID of the project whose treasury should be forwarded the splits payer contract's received payment leftovers after distributing to the default splits group.\n    @param _defaultBeneficiary The address that'll receive the project's tokens when the splits payer receives payments. \n    @param _defaultPreferClaimedTokens A flag indicating whether issued tokens from the splits payer's received payments should be automatically claimed into the beneficiary's wallet. \n    @param _defaultMemo The memo that'll be forwarded with the splits payer's received payments. \n    @param _defaultMetadata The metadata that'll be forwarded with the splits payer's received payments. \n    @param _defaultPreferAddToBalance A flag indicating if received payments should call the `pay` function or the `addToBalance` function of a project.\n    @param _owner The address that will own the splits payer.\n\n    @return splitsPayer The splits payer contract.\n  */\n  function deploySplitsPayer(\n    uint256 _defaultSplitsProjectId,\n    uint256 _defaultSplitsDomain,\n    uint256 _defaultSplitsGroup,\n    IJBSplitsStore _splitsStore,\n    uint256 _defaultProjectId,\n    address payable _defaultBeneficiary,\n    bool _defaultPreferClaimedTokens,\n    string memory _defaultMemo,\n    bytes memory _defaultMetadata,\n    bool _defaultPreferAddToBalance,\n    address _owner\n  ) external override returns (IJBSplitsPayer splitsPayer) {\n    // Deploy the splits payer.\n    splitsPayer = new JBETHERC20SplitsPayer(\n      _defaultSplitsProjectId,\n      _defaultSplitsDomain,\n      _defaultSplitsGroup,\n      _splitsStore,\n      _defaultProjectId,\n      _defaultBeneficiary,\n      _defaultPreferClaimedTokens,\n      _defaultMemo,\n      _defaultMetadata,\n      _defaultPreferAddToBalance,\n      _owner\n    );\n\n    emit DeploySplitsPayer(\n      splitsPayer,\n      _defaultSplitsProjectId,\n      _defaultSplitsDomain,\n      _defaultSplitsGroup,\n      _splitsStore,\n      _defaultProjectId,\n      _defaultBeneficiary,\n      _defaultPreferClaimedTokens,\n      _defaultMemo,\n      _defaultMetadata,\n      _defaultPreferAddToBalance,\n      _owner,\n      msg.sender\n    );\n  }\n}\n"
    },
    "contracts/enums/JBBallotState.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nenum JBBallotState {\n  Active,\n  Approved,\n  Failed\n}\n"
    },
    "contracts/interfaces/IJBDirectory.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './IJBFundingCycleStore.sol';\nimport './IJBPaymentTerminal.sol';\nimport './IJBProjects.sol';\n\ninterface IJBDirectory {\n  event SetController(uint256 indexed projectId, address indexed controller, address caller);\n\n  event AddTerminal(uint256 indexed projectId, IJBPaymentTerminal indexed terminal, address caller);\n\n  event SetTerminals(uint256 indexed projectId, IJBPaymentTerminal[] terminals, address caller);\n\n  event SetPrimaryTerminal(\n    uint256 indexed projectId,\n    address indexed token,\n    IJBPaymentTerminal indexed terminal,\n    address caller\n  );\n\n  event SetIsAllowedToSetFirstController(address indexed addr, bool indexed flag, address caller);\n\n  function projects() external view returns (IJBProjects);\n\n  function fundingCycleStore() external view returns (IJBFundingCycleStore);\n\n  function controllerOf(uint256 _projectId) external view returns (address);\n\n  function isAllowedToSetFirstController(address _address) external view returns (bool);\n\n  function terminalsOf(uint256 _projectId) external view returns (IJBPaymentTerminal[] memory);\n\n  function isTerminalOf(uint256 _projectId, IJBPaymentTerminal _terminal)\n    external\n    view\n    returns (bool);\n\n  function primaryTerminalOf(uint256 _projectId, address _token)\n    external\n    view\n    returns (IJBPaymentTerminal);\n\n  function setControllerOf(uint256 _projectId, address _controller) external;\n\n  function setTerminalsOf(uint256 _projectId, IJBPaymentTerminal[] calldata _terminals) external;\n\n  function setPrimaryTerminalOf(\n    uint256 _projectId,\n    address _token,\n    IJBPaymentTerminal _terminal\n  ) external;\n\n  function setIsAllowedToSetFirstController(address _address, bool _flag) external;\n}\n"
    },
    "contracts/interfaces/IJBETHERC20SplitsPayerDeployer.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './IJBSplitsPayer.sol';\nimport './IJBSplitsStore.sol';\n\ninterface IJBETHERC20SplitsPayerDeployer {\n  event DeploySplitsPayer(\n    IJBSplitsPayer indexed splitsPayer,\n    uint256 defaultSplitsProjectId,\n    uint256 defaultSplitsDomain,\n    uint256 defaultSplitsGroup,\n    IJBSplitsStore splitsStore,\n    uint256 defaultProjectId,\n    address defaultBeneficiary,\n    bool defaultPreferClaimedTokens,\n    string defaultMemo,\n    bytes defaultMetadata,\n    bool preferAddToBalance,\n    address owner,\n    address caller\n  );\n\n  function deploySplitsPayer(\n    uint256 _defaultSplitsProjectId,\n    uint256 _defaultSplitsDomain,\n    uint256 _defaultSplitsGroup,\n    IJBSplitsStore _splitsStore,\n    uint256 _defaultProjectId,\n    address payable _defaultBeneficiary,\n    bool _defaultPreferClaimedTokens,\n    string calldata _defaultMemo,\n    bytes calldata _defaultMetadata,\n    bool _preferAddToBalance,\n    address _owner\n  ) external returns (IJBSplitsPayer splitsPayer);\n}\n"
    },
    "contracts/interfaces/IJBFundingCycleBallot.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './../enums/JBBallotState.sol';\nimport './IJBFundingCycleStore.sol';\n\ninterface IJBFundingCycleBallot is IERC165 {\n  function duration() external view returns (uint256);\n\n  function stateOf(\n    uint256 _projectId,\n    uint256 _configuration,\n    uint256 _start\n  ) external view returns (JBBallotState);\n}\n"
    },
    "contracts/interfaces/IJBFundingCycleStore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../enums/JBBallotState.sol';\nimport './../structs/JBFundingCycle.sol';\nimport './../structs/JBFundingCycleData.sol';\n\ninterface IJBFundingCycleStore {\n  event Configure(\n    uint256 indexed configuration,\n    uint256 indexed projectId,\n    JBFundingCycleData data,\n    uint256 metadata,\n    uint256 mustStartAtOrAfter,\n    address caller\n  );\n\n  event Init(uint256 indexed configuration, uint256 indexed projectId, uint256 indexed basedOn);\n\n  function latestConfigurationOf(uint256 _projectId) external view returns (uint256);\n\n  function get(uint256 _projectId, uint256 _configuration)\n    external\n    view\n    returns (JBFundingCycle memory);\n\n  function latestConfiguredOf(uint256 _projectId)\n    external\n    view\n    returns (JBFundingCycle memory fundingCycle, JBBallotState ballotState);\n\n  function queuedOf(uint256 _projectId) external view returns (JBFundingCycle memory fundingCycle);\n\n  function currentOf(uint256 _projectId) external view returns (JBFundingCycle memory fundingCycle);\n\n  function currentBallotStateOf(uint256 _projectId) external view returns (JBBallotState);\n\n  function configureFor(\n    uint256 _projectId,\n    JBFundingCycleData calldata _data,\n    uint256 _metadata,\n    uint256 _mustStartAtOrAfter\n  ) external returns (JBFundingCycle memory fundingCycle);\n}\n"
    },
    "contracts/interfaces/IJBPaymentTerminal.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\n\ninterface IJBPaymentTerminal is IERC165 {\n  function acceptsToken(address _token) external view returns (bool);\n\n  function currencyForToken(address _token) external view returns (uint256);\n\n  function decimalsForToken(address _token) external view returns (uint256);\n\n  // Return value must be a fixed point number with 18 decimals.\n  function currentEthOverflowOf(uint256 _projectId) external view returns (uint256);\n\n  function pay(\n    uint256 _projectId,\n    uint256 _amount,\n    address _token,\n    address _beneficiary,\n    uint256 _minReturnedTokens,\n    bool _preferClaimedTokens,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) external payable returns (uint256 beneficiaryTokenCount);\n\n  function addToBalanceOf(\n    uint256 _projectId,\n    uint256 _amount,\n    address _token,\n    string calldata _memo,\n    bytes calldata _metadata\n  ) external payable;\n}\n"
    },
    "contracts/interfaces/IJBProjectPayer.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './IJBDirectory.sol';\n\ninterface IJBProjectPayer is IERC165 {\n  event SetDefaultValues(\n    uint256 indexed projectId,\n    address indexed beneficiary,\n    bool preferClaimedTokens,\n    string memo,\n    bytes metadata,\n    bool preferAddToBalance,\n    address caller\n  );\n\n  function directory() external view returns (IJBDirectory);\n\n  function defaultProjectId() external view returns (uint256);\n\n  function defaultBeneficiary() external view returns (address payable);\n\n  function defaultPreferClaimedTokens() external view returns (bool);\n\n  function defaultMemo() external view returns (string memory);\n\n  function defaultMetadata() external view returns (bytes memory);\n\n  function defaultPreferAddToBalance() external view returns (bool);\n\n  function setDefaultValues(\n    uint256 _projectId,\n    address payable _beneficiary,\n    bool _preferClaimedTokens,\n    string memory _memo,\n    bytes memory _metadata,\n    bool _defaultPreferAddToBalance\n  ) external;\n\n  function pay(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    address _beneficiary,\n    uint256 _minReturnedTokens,\n    bool _preferClaimedTokens,\n    string memory _memo,\n    bytes memory _metadata\n  ) external payable;\n\n  function addToBalanceOf(\n    uint256 _projectId,\n    address _token,\n    uint256 _amount,\n    uint256 _decimals,\n    string memory _memo,\n    bytes memory _metadata\n  ) external payable;\n\n  receive() external payable;\n}\n"
    },
    "contracts/interfaces/IJBProjects.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/token/ERC721/IERC721.sol';\nimport './../structs/JBProjectMetadata.sol';\nimport './IJBTokenUriResolver.sol';\n\ninterface IJBProjects is IERC721 {\n  event Create(\n    uint256 indexed projectId,\n    address indexed owner,\n    JBProjectMetadata metadata,\n    address caller\n  );\n\n  event SetMetadata(uint256 indexed projectId, JBProjectMetadata metadata, address caller);\n\n  event SetTokenUriResolver(IJBTokenUriResolver indexed resolver, address caller);\n\n  function count() external view returns (uint256);\n\n  function metadataContentOf(uint256 _projectId, uint256 _domain)\n    external\n    view\n    returns (string memory);\n\n  function tokenUriResolver() external view returns (IJBTokenUriResolver);\n\n  function createFor(address _owner, JBProjectMetadata calldata _metadata)\n    external\n    returns (uint256 projectId);\n\n  function setMetadataOf(uint256 _projectId, JBProjectMetadata calldata _metadata) external;\n\n  function setTokenUriResolver(IJBTokenUriResolver _newResolver) external;\n}\n"
    },
    "contracts/interfaces/IJBSplitAllocator.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport '../structs/JBSplitAllocationData.sol';\n\ninterface IJBSplitAllocator is IERC165 {\n  function allocate(JBSplitAllocationData calldata _data) external payable;\n}\n"
    },
    "contracts/interfaces/IJBSplitsPayer.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport '@openzeppelin/contracts/utils/introspection/IERC165.sol';\nimport './../structs/JBSplit.sol';\nimport './IJBSplitsStore.sol';\n\ninterface IJBSplitsPayer is IERC165 {\n  event SetDefaultSplits(\n    uint256 indexed projectId,\n    uint256 indexed domain,\n    uint256 indexed group,\n    address caller\n  );\n  event Pay(\n    uint256 indexed projectId,\n    address beneficiary,\n    address token,\n    uint256 amount,\n    uint256 decimals,\n    uint256 leftoverAmount,\n    uint256 minReturnedTokens,\n    bool preferClaimedTokens,\n    string memo,\n    bytes metadata,\n    address caller\n  );\n\n  event AddToBalance(\n    uint256 indexed projectId,\n    address beneficiary,\n    address token,\n    uint256 amount,\n    uint256 decimals,\n    uint256 leftoverAmount,\n    string memo,\n    bytes metadata,\n    address caller\n  );\n\n  event DistributeToSplit(\n    uint256 indexed projectId,\n    uint256 indexed domain,\n    uint256 indexed group,\n    JBSplit split,\n    uint256 amount,\n    address caller\n  );\n\n  function defaultSplitsProjectId() external view returns (uint256);\n\n  function defaultSplitsDomain() external view returns (uint256);\n\n  function defaultSplitsGroup() external view returns (uint256);\n\n  function splitsStore() external view returns (IJBSplitsStore);\n\n  function setDefaultSplits(\n    uint256 _projectId,\n    uint256 _domain,\n    uint256 _group\n  ) external;\n}\n"
    },
    "contracts/interfaces/IJBSplitsStore.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../structs/JBSplit.sol';\nimport './IJBDirectory.sol';\nimport './IJBProjects.sol';\n\ninterface IJBSplitsStore {\n  event SetSplit(\n    uint256 indexed projectId,\n    uint256 indexed domain,\n    uint256 indexed group,\n    JBSplit split,\n    address caller\n  );\n\n  function projects() external view returns (IJBProjects);\n\n  function directory() external view returns (IJBDirectory);\n\n  function splitsOf(\n    uint256 _projectId,\n    uint256 _domain,\n    uint256 _group\n  ) external view returns (JBSplit[] memory);\n\n  function set(\n    uint256 _projectId,\n    uint256 _domain,\n    uint256 _group,\n    JBSplit[] memory _splits\n  ) external;\n}\n"
    },
    "contracts/interfaces/IJBTokenUriResolver.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\ninterface IJBTokenUriResolver {\n  function getUri(uint256 _projectId) external view returns (string memory tokenUri);\n}\n"
    },
    "contracts/libraries/JBConstants.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\n/**\n  @notice\n  Global constants used across Juicebox contracts.\n*/\nlibrary JBConstants {\n  uint256 public constant MAX_RESERVED_RATE = 10000;\n  uint256 public constant MAX_REDEMPTION_RATE = 10000;\n  uint256 public constant MAX_DISCOUNT_RATE = 1000000000;\n  uint256 public constant SPLITS_TOTAL_PERCENT = 1000000000;\n  uint256 public constant MAX_FEE = 1000000000;\n  uint256 public constant MAX_FEE_DISCOUNT = 1000000000;\n}\n"
    },
    "contracts/libraries/JBTokens.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nlibrary JBTokens {\n  /** \n    @notice \n    The ETH token address in Juicebox is represented by 0x000000000000000000000000000000000000EEEe.\n  */\n  address public constant ETH = address(0x000000000000000000000000000000000000EEEe);\n}\n"
    },
    "contracts/structs/JBFundingCycle.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBFundingCycleBallot.sol';\n\n/** \n  @member number The funding cycle number for the cycle's project. Each funding cycle has a number that is an increment of the cycle that directly preceded it. Each project's first funding cycle has a number of 1.\n  @member configuration The timestamp when the parameters for this funding cycle were configured. This value will stay the same for subsequent funding cycles that roll over from an originally configured cycle.\n  @member basedOn The `configuration` of the funding cycle that was active when this cycle was created.\n  @member start The timestamp marking the moment from which the funding cycle is considered active. It is a unix timestamp measured in seconds.\n  @member duration The number of seconds the funding cycle lasts for, after which a new funding cycle will start. A duration of 0 means that the funding cycle will stay active until the project owner explicitly issues a reconfiguration, at which point a new funding cycle will immediately start with the updated properties. If the duration is greater than 0, a project owner cannot make changes to a funding cycle's parameters while it is active  any proposed changes will apply to the subsequent cycle. If no changes are proposed, a funding cycle rolls over to another one with the same properties but new `start` timestamp and a discounted `weight`.\n  @member weight A fixed point number with 18 decimals that contracts can use to base arbitrary calculations on. For example, payment terminals can use this to determine how many tokens should be minted when a payment is received.\n  @member discountRate A percent by how much the `weight` of the subsequent funding cycle should be reduced, if the project owner hasn't configured the subsequent funding cycle with an explicit `weight`. If it's 0, each funding cycle will have equal weight. If the number is 90%, the next funding cycle will have a 10% smaller weight. This weight is out of `JBConstants.MAX_DISCOUNT_RATE`.\n  @member ballot An address of a contract that says whether a proposed reconfiguration should be accepted or rejected. It can be used to create rules around how a project owner can change funding cycle parameters over time.\n  @member metadata Extra data that can be associated with a funding cycle.\n*/\nstruct JBFundingCycle {\n  uint256 number;\n  uint256 configuration;\n  uint256 basedOn;\n  uint256 start;\n  uint256 duration;\n  uint256 weight;\n  uint256 discountRate;\n  IJBFundingCycleBallot ballot;\n  uint256 metadata;\n}\n"
    },
    "contracts/structs/JBFundingCycleData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBFundingCycleBallot.sol';\n\n/** \n  @member duration The number of seconds the funding cycle lasts for, after which a new funding cycle will start. A duration of 0 means that the funding cycle will stay active until the project owner explicitly issues a reconfiguration, at which point a new funding cycle will immediately start with the updated properties. If the duration is greater than 0, a project owner cannot make changes to a funding cycle's parameters while it is active  any proposed changes will apply to the subsequent cycle. If no changes are proposed, a funding cycle rolls over to another one with the same properties but new `start` timestamp and a discounted `weight`.\n  @member weight A fixed point number with 18 decimals that contracts can use to base arbitrary calculations on. For example, payment terminals can use this to determine how many tokens should be minted when a payment is received.\n  @member discountRate A percent by how much the `weight` of the subsequent funding cycle should be reduced, if the project owner hasn't configured the subsequent funding cycle with an explicit `weight`. If it's 0, each funding cycle will have equal weight. If the number is 90%, the next funding cycle will have a 10% smaller weight. This weight is out of `JBConstants.MAX_DISCOUNT_RATE`.\n  @member ballot An address of a contract that says whether a proposed reconfiguration should be accepted or rejected. It can be used to create rules around how a project owner can change funding cycle parameters over time.\n*/\nstruct JBFundingCycleData {\n  uint256 duration;\n  uint256 weight;\n  uint256 discountRate;\n  IJBFundingCycleBallot ballot;\n}\n"
    },
    "contracts/structs/JBProjectMetadata.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\n/** \n  @member content The metadata content.\n  @member domain The domain within which the metadata applies.\n*/\nstruct JBProjectMetadata {\n  string content;\n  uint256 domain;\n}\n"
    },
    "contracts/structs/JBSplit.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './../interfaces/IJBSplitAllocator.sol';\n\n/** \n  @member preferClaimed A flag that only has effect if a projectId is also specified, and the project has a token contract attached. If so, this flag indicates if the tokens that result from making a payment to the project should be delivered claimed into the beneficiary's wallet, or unclaimed to save gas.\n  @member preferAddToBalance A flag indicating if a distribution to a project should prefer triggering it's addToBalance function instead of its pay function.\n  @member percent The percent of the whole group that this split occupies. This number is out of `JBConstants.SPLITS_TOTAL_PERCENT`.\n  @member projectId The ID of a project. If an allocator is not set but a projectId is set, funds will be sent to the protocol treasury belonging to the project who's ID is specified. Resulting tokens will be routed to the beneficiary with the claimed token preference respected.\n  @member beneficiary An address. The role the of the beneficary depends on whether or not projectId is specified, and whether or not an allocator is specified. If allocator is set, the beneficiary will be forwarded to the allocator for it to use. If allocator is not set but projectId is set, the beneficiary is the address to which the project's tokens will be sent that result from a payment to it. If neither allocator or projectId are set, the beneficiary is where the funds from the split will be sent.\n  @member lockedUntil Specifies if the split should be unchangeable until the specified time, with the exception of extending the locked period.\n  @member allocator If an allocator is specified, funds will be sent to the allocator contract along with all properties of this split.\n*/\nstruct JBSplit {\n  bool preferClaimed;\n  bool preferAddToBalance;\n  uint256 percent;\n  uint256 projectId;\n  address payable beneficiary;\n  uint256 lockedUntil;\n  IJBSplitAllocator allocator;\n}\n"
    },
    "contracts/structs/JBSplitAllocationData.sol": {
      "content": "// SPDX-License-Identifier: MIT\npragma solidity 0.8.6;\n\nimport './JBSplit.sol';\n\n/** \n  @member token The token being sent to the split allocator.\n  @member amount The amount being sent to the split allocator, as a fixed point number.\n  @member decimals The number of decimals in the amount.\n  @member projectId The project to which the split belongs.\n  @member group The group to which the split belongs.\n  @member split The split that caused the allocation.\n*/\nstruct JBSplitAllocationData {\n  address token;\n  uint256 amount;\n  uint256 decimals;\n  uint256 projectId;\n  uint256 group;\n  JBSplit split;\n}\n"
    },
    "prb-math/contracts/PRBMath.sol": {
      "content": "// SPDX-License-Identifier: Unlicense\npragma solidity >=0.8.4;\n\n/// @notice Emitted when the result overflows uint256.\nerror PRBMath__MulDivFixedPointOverflow(uint256 prod1);\n\n/// @notice Emitted when the result overflows uint256.\nerror PRBMath__MulDivOverflow(uint256 prod1, uint256 denominator);\n\n/// @notice Emitted when one of the inputs is type(int256).min.\nerror PRBMath__MulDivSignedInputTooSmall();\n\n/// @notice Emitted when the intermediary absolute result overflows int256.\nerror PRBMath__MulDivSignedOverflow(uint256 rAbs);\n\n/// @notice Emitted when the input is MIN_SD59x18.\nerror PRBMathSD59x18__AbsInputTooSmall();\n\n/// @notice Emitted when ceiling a number overflows SD59x18.\nerror PRBMathSD59x18__CeilOverflow(int256 x);\n\n/// @notice Emitted when one of the inputs is MIN_SD59x18.\nerror PRBMathSD59x18__DivInputTooSmall();\n\n/// @notice Emitted when one of the intermediary unsigned results overflows SD59x18.\nerror PRBMathSD59x18__DivOverflow(uint256 rAbs);\n\n/// @notice Emitted when the input is greater than 133.084258667509499441.\nerror PRBMathSD59x18__ExpInputTooBig(int256 x);\n\n/// @notice Emitted when the input is greater than 192.\nerror PRBMathSD59x18__Exp2InputTooBig(int256 x);\n\n/// @notice Emitted when flooring a number underflows SD59x18.\nerror PRBMathSD59x18__FloorUnderflow(int256 x);\n\n/// @notice Emitted when converting a basic integer to the fixed-point format overflows SD59x18.\nerror PRBMathSD59x18__FromIntOverflow(int256 x);\n\n/// @notice Emitted when converting a basic integer to the fixed-point format underflows SD59x18.\nerror PRBMathSD59x18__FromIntUnderflow(int256 x);\n\n/// @notice Emitted when the product of the inputs is negative.\nerror PRBMathSD59x18__GmNegativeProduct(int256 x, int256 y);\n\n/// @notice Emitted when multiplying the inputs overflows SD59x18.\nerror PRBMathSD59x18__GmOverflow(int256 x, int256 y);\n\n/// @notice Emitted when the input is less than or equal to zero.\nerror PRBMathSD59x18__LogInputTooSmall(int256 x);\n\n/// @notice Emitted when one of the inputs is MIN_SD59x18.\nerror PRBMathSD59x18__MulInputTooSmall();\n\n/// @notice Emitted when the intermediary absolute result overflows SD59x18.\nerror PRBMathSD59x18__MulOverflow(uint256 rAbs);\n\n/// @notice Emitted when the intermediary absolute result overflows SD59x18.\nerror PRBMathSD59x18__PowuOverflow(uint256 rAbs);\n\n/// @notice Emitted when the input is negative.\nerror PRBMathSD59x18__SqrtNegativeInput(int256 x);\n\n/// @notice Emitted when the calculating the square root overflows SD59x18.\nerror PRBMathSD59x18__SqrtOverflow(int256 x);\n\n/// @notice Emitted when addition overflows UD60x18.\nerror PRBMathUD60x18__AddOverflow(uint256 x, uint256 y);\n\n/// @notice Emitted when ceiling a number overflows UD60x18.\nerror PRBMathUD60x18__CeilOverflow(uint256 x);\n\n/// @notice Emitted when the input is greater than 133.084258667509499441.\nerror PRBMathUD60x18__ExpInputTooBig(uint256 x);\n\n/// @notice Emitted when the input is greater than 192.\nerror PRBMathUD60x18__Exp2InputTooBig(uint256 x);\n\n/// @notice Emitted when converting a basic integer to the fixed-point format format overflows UD60x18.\nerror PRBMathUD60x18__FromUintOverflow(uint256 x);\n\n/// @notice Emitted when multiplying the inputs overflows UD60x18.\nerror PRBMathUD60x18__GmOverflow(uint256 x, uint256 y);\n\n/// @notice Emitted when the input is less than 1.\nerror PRBMathUD60x18__LogInputTooSmall(uint256 x);\n\n/// @notice Emitted when the calculating the square root overflows UD60x18.\nerror PRBMathUD60x18__SqrtOverflow(uint256 x);\n\n/// @notice Emitted when subtraction underflows UD60x18.\nerror PRBMathUD60x18__SubUnderflow(uint256 x, uint256 y);\n\n/// @dev Common mathematical functions used in both PRBMathSD59x18 and PRBMathUD60x18. Note that this shared library\n/// does not always assume the signed 59.18-decimal fixed-point or the unsigned 60.18-decimal fixed-point\n/// representation. When it does not, it is explicitly mentioned in the NatSpec documentation.\nlibrary PRBMath {\n    /// STRUCTS ///\n\n    struct SD59x18 {\n        int256 value;\n    }\n\n    struct UD60x18 {\n        uint256 value;\n    }\n\n    /// STORAGE ///\n\n    /// @dev How many trailing decimals can be represented.\n    uint256 internal constant SCALE = 1e18;\n\n    /// @dev Largest power of two divisor of SCALE.\n    uint256 internal constant SCALE_LPOTD = 262144;\n\n    /// @dev SCALE inverted mod 2^256.\n    uint256 internal constant SCALE_INVERSE =\n        78156646155174841979727994598816262306175212592076161876661_508869554232690281;\n\n    /// FUNCTIONS ///\n\n    /// @notice Calculates the binary exponent of x using the binary fraction method.\n    /// @dev Has to use 192.64-bit fixed-point numbers.\n    /// See https://ethereum.stackexchange.com/a/96594/24693.\n    /// @param x The exponent as an unsigned 192.64-bit fixed-point number.\n    /// @return result The result as an unsigned 60.18-decimal fixed-point number.\n    function exp2(uint256 x) internal pure returns (uint256 result) {\n        unchecked {\n            // Start from 0.5 in the 192.64-bit fixed-point format.\n            result = 0x800000000000000000000000000000000000000000000000;\n\n            // Multiply the result by root(2, 2^-i) when the bit at position i is 1. None of the intermediary results overflows\n            // because the initial result is 2^191 and all magic factors are less than 2^65.\n            if (x & 0x8000000000000000 > 0) {\n                result = (result * 0x16A09E667F3BCC909) >> 64;\n            }\n            if (x & 0x4000000000000000 > 0) {\n                result = (result * 0x1306FE0A31B7152DF) >> 64;\n            }\n            if (x & 0x2000000000000000 > 0) {\n                result = (result * 0x1172B83C7D517ADCE) >> 64;\n            }\n            if (x & 0x1000000000000000 > 0) {\n                result = (result * 0x10B5586CF9890F62A) >> 64;\n            }\n            if (x & 0x800000000000000 > 0) {\n                result = (result * 0x1059B0D31585743AE) >> 64;\n            }\n            if (x & 0x400000000000000 > 0) {\n                result = (result * 0x102C9A3E778060EE7) >> 64;\n            }\n            if (x & 0x200000000000000 > 0) {\n                result = (result * 0x10163DA9FB33356D8) >> 64;\n            }\n            if (x & 0x100000000000000 > 0) {\n                result = (result * 0x100B1AFA5ABCBED61) >> 64;\n            }\n            if (x & 0x80000000000000 > 0) {\n                result = (result * 0x10058C86DA1C09EA2) >> 64;\n            }\n            if (x & 0x40000000000000 > 0) {\n                result = (result * 0x1002C605E2E8CEC50) >> 64;\n            }\n            if (x & 0x20000000000000 > 0) {\n                result = (result * 0x100162F3904051FA1) >> 64;\n            }\n            if (x & 0x10000000000000 > 0) {\n                result = (result * 0x1000B175EFFDC76BA) >> 64;\n            }\n            if (x & 0x8000000000000 > 0) {\n                result = (result * 0x100058BA01FB9F96D) >> 64;\n            }\n            if (x & 0x4000000000000 > 0) {\n                result = (result * 0x10002C5CC37DA9492) >> 64;\n            }\n            if (x & 0x2000000000000 > 0) {\n                result = (result * 0x1000162E525EE0547) >> 64;\n            }\n            if (x & 0x1000000000000 > 0) {\n                result = (result * 0x10000B17255775C04) >> 64;\n            }\n            if (x & 0x800000000000 > 0) {\n                result = (result * 0x1000058B91B5BC9AE) >> 64;\n            }\n            if (x & 0x400000000000 > 0) {\n                result = (result * 0x100002C5C89D5EC6D) >> 64;\n            }\n            if (x & 0x200000000000 > 0) {\n                result = (result * 0x10000162E43F4F831) >> 64;\n            }\n            if (x & 0x100000000000 > 0) {\n                result = (result * 0x100000B1721BCFC9A) >> 64;\n            }\n            if (x & 0x80000000000 > 0) {\n                result = (result * 0x10000058B90CF1E6E) >> 64;\n            }\n            if (x & 0x40000000000 > 0) {\n                result = (result * 0x1000002C5C863B73F) >> 64;\n            }\n            if (x & 0x20000000000 > 0) {\n                result = (result * 0x100000162E430E5A2) >> 64;\n            }\n            if (x & 0x10000000000 > 0) {\n                result = (result * 0x1000000B172183551) >> 64;\n            }\n            if (x & 0x8000000000 > 0) {\n                result = (result * 0x100000058B90C0B49) >> 64;\n            }\n            if (x & 0x4000000000 > 0) {\n                result = (result * 0x10000002C5C8601CC) >> 64;\n            }\n            if (x & 0x2000000000 > 0) {\n                result = (result * 0x1000000162E42FFF0) >> 64;\n            }\n            if (x & 0x1000000000 > 0) {\n                result = (result * 0x10000000B17217FBB) >> 64;\n            }\n            if (x & 0x800000000 > 0) {\n                result = (result * 0x1000000058B90BFCE) >> 64;\n            }\n            if (x & 0x400000000 > 0) {\n                result = (result * 0x100000002C5C85FE3) >> 64;\n            }\n            if (x & 0x200000000 > 0) {\n                result = (result * 0x10000000162E42FF1) >> 64;\n            }\n            if (x & 0x100000000 > 0) {\n                result = (result * 0x100000000B17217F8) >> 64;\n            }\n            if (x & 0x80000000 > 0) {\n                result = (result * 0x10000000058B90BFC) >> 64;\n            }\n            if (x & 0x40000000 > 0) {\n                result = (result * 0x1000000002C5C85FE) >> 64;\n            }\n            if (x & 0x20000000 > 0) {\n                result = (result * 0x100000000162E42FF) >> 64;\n            }\n            if (x & 0x10000000 > 0) {\n                result = (result * 0x1000000000B17217F) >> 64;\n            }\n            if (x & 0x8000000 > 0) {\n                result = (result * 0x100000000058B90C0) >> 64;\n            }\n            if (x & 0x4000000 > 0) {\n                result = (result * 0x10000000002C5C860) >> 64;\n            }\n            if (x & 0x2000000 > 0) {\n                result = (result * 0x1000000000162E430) >> 64;\n            }\n            if (x & 0x1000000 > 0) {\n                result = (result * 0x10000000000B17218) >> 64;\n            }\n            if (x & 0x800000 > 0) {\n                result = (result * 0x1000000000058B90C) >> 64;\n            }\n            if (x & 0x400000 > 0) {\n                result = (result * 0x100000000002C5C86) >> 64;\n            }\n            if (x & 0x200000 > 0) {\n                result = (result * 0x10000000000162E43) >> 64;\n            }\n            if (x & 0x100000 > 0) {\n                result = (result * 0x100000000000B1721) >> 64;\n            }\n            if (x & 0x80000 > 0) {\n                result = (result * 0x10000000000058B91) >> 64;\n            }\n            if (x & 0x40000 > 0) {\n                result = (result * 0x1000000000002C5C8) >> 64;\n            }\n            if (x & 0x20000 > 0) {\n                result = (result * 0x100000000000162E4) >> 64;\n            }\n            if (x & 0x10000 > 0) {\n                result = (result * 0x1000000000000B172) >> 64;\n            }\n            if (x & 0x8000 > 0) {\n                result = (result * 0x100000000000058B9) >> 64;\n            }\n            if (x & 0x4000 > 0) {\n                result = (result * 0x10000000000002C5D) >> 64;\n            }\n            if (x & 0x2000 > 0) {\n                result = (result * 0x1000000000000162E) >> 64;\n            }\n            if (x & 0x1000 > 0) {\n                result = (result * 0x10000000000000B17) >> 64;\n            }\n            if (x & 0x800 > 0) {\n                result = (result * 0x1000000000000058C) >> 64;\n            }\n            if (x & 0x400 > 0) {\n                result = (result * 0x100000000000002C6) >> 64;\n            }\n            if (x & 0x200 > 0) {\n                result = (result * 0x10000000000000163) >> 64;\n            }\n            if (x & 0x100 > 0) {\n                result = (result * 0x100000000000000B1) >> 64;\n            }\n            if (x & 0x80 > 0) {\n                result = (result * 0x10000000000000059) >> 64;\n            }\n            if (x & 0x40 > 0) {\n                result = (result * 0x1000000000000002C) >> 64;\n            }\n            if (x & 0x20 > 0) {\n                result = (result * 0x10000000000000016) >> 64;\n            }\n            if (x & 0x10 > 0) {\n                result = (result * 0x1000000000000000B) >> 64;\n            }\n            if (x & 0x8 > 0) {\n                result = (result * 0x10000000000000006) >> 64;\n            }\n            if (x & 0x4 > 0) {\n                result = (result * 0x10000000000000003) >> 64;\n            }\n            if (x & 0x2 > 0) {\n                result = (result * 0x10000000000000001) >> 64;\n            }\n            if (x & 0x1 > 0) {\n                result = (result * 0x10000000000000001) >> 64;\n            }\n\n            // We're doing two things at the same time:\n            //\n            //   1. Multiply the result by 2^n + 1, where \"2^n\" is the integer part and the one is added to account for\n            //      the fact that we initially set the result to 0.5. This is accomplished by subtracting from 191\n            //      rather than 192.\n            //   2. Convert the result to the unsigned 60.18-decimal fixed-point format.\n            //\n            // This works because 2^(191-ip) = 2^ip / 2^191, where \"ip\" is the integer part \"2^n\".\n            result *= SCALE;\n            result >>= (191 - (x >> 64));\n        }\n    }\n\n    /// @notice Finds the zero-based index of the first one in the binary representation of x.\n    /// @dev See the note on msb in the \"Find First Set\" Wikipedia article https://en.wikipedia.org/wiki/Find_first_set\n    /// @param x The uint256 number for which to find the index of the most significant bit.\n    /// @return msb The index of the most significant bit as an uint256.\n    function mostSignificantBit(uint256 x) internal pure returns (uint256 msb) {\n        if (x >= 2**128) {\n            x >>= 128;\n            msb += 128;\n        }\n        if (x >= 2**64) {\n            x >>= 64;\n            msb += 64;\n        }\n        if (x >= 2**32) {\n            x >>= 32;\n            msb += 32;\n        }\n        if (x >= 2**16) {\n            x >>= 16;\n            msb += 16;\n        }\n        if (x >= 2**8) {\n            x >>= 8;\n            msb += 8;\n        }\n        if (x >= 2**4) {\n            x >>= 4;\n            msb += 4;\n        }\n        if (x >= 2**2) {\n            x >>= 2;\n            msb += 2;\n        }\n        if (x >= 2**1) {\n            // No need to shift x any more.\n            msb += 1;\n        }\n    }\n\n    /// @notice Calculates floor(x*ydenominator) with full precision.\n    ///\n    /// @dev Credit to Remco Bloemen under MIT license https://xn--2-umb.com/21/muldiv.\n    ///\n    /// Requirements:\n    /// - The denominator cannot be zero.\n    /// - The result must fit within uint256.\n    ///\n    /// Caveats:\n    /// - This function does not work with fixed-point numbers.\n    ///\n    /// @param x The multiplicand as an uint256.\n    /// @param y The multiplier as an uint256.\n    /// @param denominator The divisor as an uint256.\n    /// @return result The result as an uint256.\n    function mulDiv(\n        uint256 x,\n        uint256 y,\n        uint256 denominator\n    ) internal pure returns (uint256 result) {\n        // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use\n        // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256\n        // variables such that product = prod1 * 2^256 + prod0.\n        uint256 prod0; // Least significant 256 bits of the product\n        uint256 prod1; // Most significant 256 bits of the product\n        assembly {\n            let mm := mulmod(x, y, not(0))\n            prod0 := mul(x, y)\n            prod1 := sub(sub(mm, prod0), lt(mm, prod0))\n        }\n\n        // Handle non-overflow cases, 256 by 256 division.\n        if (prod1 == 0) {\n            unchecked {\n                result = prod0 / denominator;\n            }\n            return result;\n        }\n\n        // Make sure the result is less than 2^256. Also prevents denominator == 0.\n        if (prod1 >= denominator) {\n            revert PRBMath__MulDivOverflow(prod1, denominator);\n        }\n\n        ///////////////////////////////////////////////\n        // 512 by 256 division.\n        ///////////////////////////////////////////////\n\n        // Make division exact by subtracting the remainder from [prod1 prod0].\n        uint256 remainder;\n        assembly {\n            // Compute remainder using mulmod.\n            remainder := mulmod(x, y, denominator)\n\n            // Subtract 256 bit number from 512 bit number.\n            prod1 := sub(prod1, gt(remainder, prod0))\n            prod0 := sub(prod0, remainder)\n        }\n\n        // Factor powers of two out of denominator and compute largest power of two divisor of denominator. Always >= 1.\n        // See https://cs.stackexchange.com/q/138556/92363.\n        unchecked {\n            // Does not overflow because the denominator cannot be zero at this stage in the function.\n            uint256 lpotdod = denominator & (~denominator + 1);\n            assembly {\n                // Divide denominator by lpotdod.\n                denominator := div(denominator, lpotdod)\n\n                // Divide [prod1 prod0] by lpotdod.\n                prod0 := div(prod0, lpotdod)\n\n                // Flip lpotdod such that it is 2^256 / lpotdod. If lpotdod is zero, then it becomes one.\n                lpotdod := add(div(sub(0, lpotdod), lpotdod), 1)\n            }\n\n            // Shift in bits from prod1 into prod0.\n            prod0 |= prod1 * lpotdod;\n\n            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such\n            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for\n            // four bits. That is, denominator * inv = 1 mod 2^4.\n            uint256 inverse = (3 * denominator) ^ 2;\n\n            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also works\n            // in modular arithmetic, doubling the correct bits in each step.\n            inverse *= 2 - denominator * inverse; // inverse mod 2^8\n            inverse *= 2 - denominator * inverse; // inverse mod 2^16\n            inverse *= 2 - denominator * inverse; // inverse mod 2^32\n            inverse *= 2 - denominator * inverse; // inverse mod 2^64\n            inverse *= 2 - denominator * inverse; // inverse mod 2^128\n            inverse *= 2 - denominator * inverse; // inverse mod 2^256\n\n            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.\n            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is\n            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1\n            // is no longer required.\n            result = prod0 * inverse;\n            return result;\n        }\n    }\n\n    /// @notice Calculates floor(x*y1e18) with full precision.\n    ///\n    /// @dev Variant of \"mulDiv\" with constant folding, i.e. in which the denominator is always 1e18. Before returning the\n    /// final result, we add 1 if (x * y) % SCALE >= HALF_SCALE. Without this, 6.6e-19 would be truncated to 0 instead of\n    /// being rounded to 1e-18.  See \"Listing 6\" and text above it at https://accu.org/index.php/journals/1717.\n    ///\n    /// Requirements:\n    /// - The result must fit within uint256.\n    ///\n    /// Caveats:\n    /// - The body is purposely left uncommented; see the NatSpec comments in \"PRBMath.mulDiv\" to understand how this works.\n    /// - It is assumed that the result can never be type(uint256).max when x and y solve the following two equations:\n    ///     1. x * y = type(uint256).max * SCALE\n    ///     2. (x * y) % SCALE >= SCALE / 2\n    ///\n    /// @param x The multiplicand as an unsigned 60.18-decimal fixed-point number.\n    /// @param y The multiplier as an unsigned 60.18-decimal fixed-point number.\n    /// @return result The result as an unsigned 60.18-decimal fixed-point number.\n    function mulDivFixedPoint(uint256 x, uint256 y) internal pure returns (uint256 result) {\n        uint256 prod0;\n        uint256 prod1;\n        assembly {\n            let mm := mulmod(x, y, not(0))\n            prod0 := mul(x, y)\n            prod1 := sub(sub(mm, prod0), lt(mm, prod0))\n        }\n\n        if (prod1 >= SCALE) {\n            revert PRBMath__MulDivFixedPointOverflow(prod1);\n        }\n\n        uint256 remainder;\n        uint256 roundUpUnit;\n        assembly {\n            remainder := mulmod(x, y, SCALE)\n            roundUpUnit := gt(remainder, 499999999999999999)\n        }\n\n        if (prod1 == 0) {\n            unchecked {\n                result = (prod0 / SCALE) + roundUpUnit;\n                return result;\n            }\n        }\n\n        assembly {\n            result := add(\n                mul(\n                    or(\n                        div(sub(prod0, remainder), SCALE_LPOTD),\n                        mul(sub(prod1, gt(remainder, prod0)), add(div(sub(0, SCALE_LPOTD), SCALE_LPOTD), 1))\n                    ),\n                    SCALE_INVERSE\n                ),\n                roundUpUnit\n            )\n        }\n    }\n\n    /// @notice Calculates floor(x*ydenominator) with full precision.\n    ///\n    /// @dev An extension of \"mulDiv\" for signed numbers. Works by computing the signs and the absolute values separately.\n    ///\n    /// Requirements:\n    /// - None of the inputs can be type(int256).min.\n    /// - The result must fit within int256.\n    ///\n    /// @param x The multiplicand as an int256.\n    /// @param y The multiplier as an int256.\n    /// @param denominator The divisor as an int256.\n    /// @return result The result as an int256.\n    function mulDivSigned(\n        int256 x,\n        int256 y,\n        int256 denominator\n    ) internal pure returns (int256 result) {\n        if (x == type(int256).min || y == type(int256).min || denominator == type(int256).min) {\n            revert PRBMath__MulDivSignedInputTooSmall();\n        }\n\n        // Get hold of the absolute values of x, y and the denominator.\n        uint256 ax;\n        uint256 ay;\n        uint256 ad;\n        unchecked {\n            ax = x < 0 ? uint256(-x) : uint256(x);\n            ay = y < 0 ? uint256(-y) : uint256(y);\n            ad = denominator < 0 ? uint256(-denominator) : uint256(denominator);\n        }\n\n        // Compute the absolute value of (x*y)denominator. The result must fit within int256.\n        uint256 rAbs = mulDiv(ax, ay, ad);\n        if (rAbs > uint256(type(int256).max)) {\n            revert PRBMath__MulDivSignedOverflow(rAbs);\n        }\n\n        // Get the signs of x, y and the denominator.\n        uint256 sx;\n        uint256 sy;\n        uint256 sd;\n        assembly {\n            sx := sgt(x, sub(0, 1))\n            sy := sgt(y, sub(0, 1))\n            sd := sgt(denominator, sub(0, 1))\n        }\n\n        // XOR over sx, sy and sd. This is checking whether there are one or three negative signs in the inputs.\n        // If yes, the result should be negative.\n        result = sx ^ sy ^ sd == 0 ? -int256(rAbs) : int256(rAbs);\n    }\n\n    /// @notice Calculates the square root of x, rounding down.\n    /// @dev Uses the Babylonian method https://en.wikipedia.org/wiki/Methods_of_computing_square_roots#Babylonian_method.\n    ///\n    /// Caveats:\n    /// - This function does not work with fixed-point numbers.\n    ///\n    /// @param x The uint256 number for which to calculate the square root.\n    /// @return result The result as an uint256.\n    function sqrt(uint256 x) internal pure returns (uint256 result) {\n        if (x == 0) {\n            return 0;\n        }\n\n        // Set the initial guess to the closest power of two that is higher than x.\n        uint256 xAux = uint256(x);\n        result = 1;\n        if (xAux >= 0x100000000000000000000000000000000) {\n            xAux >>= 128;\n            result <<= 64;\n        }\n        if (xAux >= 0x10000000000000000) {\n            xAux >>= 64;\n            result <<= 32;\n        }\n        if (xAux >= 0x100000000) {\n            xAux >>= 32;\n            result <<= 16;\n        }\n        if (xAux >= 0x10000) {\n            xAux >>= 16;\n            result <<= 8;\n        }\n        if (xAux >= 0x100) {\n            xAux >>= 8;\n            result <<= 4;\n        }\n        if (xAux >= 0x10) {\n            xAux >>= 4;\n            result <<= 2;\n        }\n        if (xAux >= 0x8) {\n            result <<= 1;\n        }\n\n        // The operations can never overflow because the result is max 2^127 when it enters this block.\n        unchecked {\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1;\n            result = (result + x / result) >> 1; // Seven iterations should be enough\n            uint256 roundedDownResult = x / result;\n            return result >= roundedDownResult ? roundedDownResult : result;\n        }\n    }\n}\n"
    }
  }
}}